app twitterAgent {
  wasp: {
    version: "^0.11.0"
  },
  title: "twitter-agent",
  client: {
    rootComponent: import App from "@client/App",
  },
  db: { 
    system: PostgreSQL,
    seeds: [
      import { runChainScript } from "@server/scripts/script.js",
      import { embedNotes } from "@server/seeding/embedNotes.js",
    ]
  },
  auth: {
    userEntity: User,
    onAuthFailedRedirectTo: "/login",
    methods: {
      usernameAndPassword: {},
    }
  },
  dependencies: [
    ("openai", "3.2.1"),
    ("gpt-3-encoder", "^1.1.4"),
    ("rettiwt-api", "1.1.8"),
    ("langchain", "0.0.96"),
    ("twitter-api-v2", "1.15.0"),
    ("@pinecone-database/pinecone", "0.1.6"),
    ("@tippyjs/react", "4.2.6"),
    ("@headlessui/react", "1.7.15"),
    ("react-icons", "4.8.0"),
    ("react-twitter-embed", "4.0.4"),
  ],
}

// ### ENTITIES ENTITIES ENTITIES
// #### ENTITIES ENTITIES ENTITIES
// ##### ENTITIES ENTITIES ENTITIES
// ###### ENTITIES ENTITIES ENTITIES

entity Tweet {=psl
  id              Int           @id @default(autoincrement())
  tweetId         String        
  author          Author       @relation(fields: [authorId], references: [id])
  authorId        String
  content         String
  tweetedAt       DateTime     @default(now())
  user            User         @relation(fields: [userId], references: [id])
  userId          Int          
  drafts          TweetDraft[]
  ideas           GeneratedIdea[]
  @@unique([tweetId, userId])
psl=}

entity Author {=psl
  id              String         @id 
  username        String
  displayName     String
  profilePic      String
  tweets          Tweet[]
psl=}

entity TweetDraft {=psl
  id              Int         @id @default(autoincrement())
  content         String
  notes           String
  originalTweet   Tweet       @relation(fields: [originalTweetId], references: [id])
  originalTweetId Int
  createdAt       DateTime    @default(now())
  user            User        @relation(fields: [userId], references: [id])
  userId          Int
psl=}

entity GeneratedIdea {=psl
  id              Int         @id @default(autoincrement())
  content         String
  createdAt       DateTime    @default(now())
  updatedAt       DateTime    @default(now())
  user            User        @relation(fields: [userId], references: [id])
  userId          Int
  originalTweet   Tweet?      @relation(fields: [originalTweetId], references: [id])
  originalTweetId Int?
  isEmbedded      Boolean     @default(false)
psl=}

entity VerifyTokens {=psl
  id              Int         @id @default(autoincrement())
  codeVerifier    String
  state           String
  user            User        @relation(fields: [userId], references: [id])
  userId          Int         
  createdAt       DateTime    @default(now())
psl=}

entity AccessTokens {=psl
  id              Int         @id @default(autoincrement())
  accessToken     String
  refreshToken    String?
  expiresIn       Int?
  updatedAt       DateTime    @default(now())
  user            User        @relation(fields: [userId], references: [id])
  userId          Int         @unique
psl=}

entity User {=psl
  id              Int         @id @default(autoincrement())
  username        String      @unique
  password        String
  createdAt       DateTime    @default(now())
  AccessTokens    AccessTokens[]
  VerifyTokens    VerifyTokens[]
  favUsers        String[]
  originalTweets  Tweet[]
  tweetDrafts     TweetDraft[]
  generatedIdeas  GeneratedIdea[]
psl=}

// <<< ROUTES ROUTES ROUTES
// <<<< ROUTES ROUTES ROUTES
// <<<<< ROUTES ROUTES ROUTES
// <<<<<< ROUTES ROUTES ROUTES

route LoginPageRoute { path: "/login", to: LoginPage }
page LoginPage {
  component: import Login from "@client/auth/LoginPage"
}

route TwitterCallbackRoute { path: "/twitter", to: CallbackPage }
page CallbackPage {
  component: import TwitterCallback from "@client/auth/TwitterCallback"
}

route GeneratedIdeasRoute { path: "/", to: GeneratedIdeasPage }
page GeneratedIdeasPage {
  authRequired: true,
  component: import GeneratedIdeas from "@client/GeneratedIdeasPage"
}

route EmbeddedIdeasRoute { path: "/embedded-ideas", to: EmbeddedIdeasPage }
page EmbeddedIdeasPage {
  authRequired: true,
  component: import EmbeddedIdeas from "@client/EmbeddedIdeasPage"
}

route SettingsRoute { path: "/settings", to: SettingsPage }
page SettingsPage {
  authRequired: true,
  component: import Settings from "@client/SettingsPage"
}

// ??? QUERIES QUERIES QUERIES
// ???? QUERIES QUERIES QUERIES
// ????? QUERIES QUERIES QUERIES
// ?????? QUERIES QUERIES QUERIES

query getEmbeddedIdeas {
  fn: import { getEmbeddedIdeas } from "@server/ideas.js",
  entities: [GeneratedIdea]
}

query getTweetDraftsWithIdeas {
  fn: import { getTweetDraftsWithIdeas } from "@server/tweets.js",
  entities: [TweetDraft, GeneratedIdea, Tweet]
}

query fetchSimilarNotes {
  fn: import { fetchSimilarNotes } from "@server/ideas.js",
  entities: [GeneratedIdea]
}

query getAccessTokens {
  fn: import { getAccessTokens } from "@server/twitterAuth.js",
  entities: [AccessTokens]
}

// !!! ACTIONS ACTIONS ACTIONS
// !!!! ACTIONS ACTIONS ACTIONS
// !!!!! ACTIONS ACTIONS ACTIONS
// !!!!!! ACTIONS ACTIONS ACTIONS

action embedIdeas {
  fn: import { embedIdeas } from "@server/ideas.js",
  entities: [GeneratedIdea]
}

action embedIdea {
  fn: import { embedIdea } from "@server/ideas.js",
  entities: [GeneratedIdea]
}

action updateIdea {
  fn: import { updateIdea } from "@server/ideas.js",
  entities: [GeneratedIdea]
}

action deleteIdea {
  fn: import { deleteIdea } from "@server/ideas.js",
  entities: [GeneratedIdea]
}

action generateTweetDraftsAndIdeas {
  fn: import { generateTweetDraftsAndIdeas } from "@server/tweets.js",
  entities: [Tweet, TweetDraft, GeneratedIdea, Author, User]
}

action generateTweet {
  fn: import { generateTweet } from "@server/tweets.js",
  entities: [Tweet, TweetDraft, GeneratedIdea, User]
}

action sendTweet {
  fn: import { sendTweet } from "@server/tweets.js",
  entities: [Tweet, TweetDraft, AccessTokens, GeneratedIdea, User]
}

action twitterAuthCallback {
  fn: import { callback } from "@server/twitterAuth.js",
  entities: [VerifyTokens, AccessTokens, User],
}

action twitterAuth {
  fn: import { twitterAuth } from "@server/twitterAuth.js",
  entities: [VerifyTokens, User],
}

action updateSettings {
  fn: import { updateSettings } from "@server/account.js",
  entities: [User]
}

// ^^^ JOBS JOBS JOBS
// ^^^^ JOBS JOBS JOBS
// ^^^^^ JOBS JOBS JOBS
// ^^^^^^ JOBS JOBS JOBS

job replyDraftsJob{
  executor: PgBoss,
  perform: {
    fn: import generateReplyDrafts from "@server/jobs/generateReplyDrafts.js"
  },
  entities: [Tweet, TweetDraft, GeneratedIdea, User, Author],
  schedule: {
    cron: "0 * * * *", // runs every hour. if it fails, it retries 2 times with 2 minute delay
    executorOptions: {
      pgBoss: {=json { "retryLimit": 2, "retryDelay": 120 } json=},
    }
  }
}
